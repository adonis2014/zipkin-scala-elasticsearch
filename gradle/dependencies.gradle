// Adopted from https://github.com/ratpack/ratpack/blob/master/gradle/dependencies.gradle

ext {
    scalaVersion = '2.11.7'
    scalaInterfaceVersion = '2.11'

    commonVersions = [
            finagle: '6.31.0',
            twitterServer: '1.16.0',
            finatra: '2.1.2',
            slf4j: '1.7.12',
            logback: '1.1.3',
            zipkin: '1.26.0',
            argonaut: '6.1',
            mustache: '0.9.1',
            chmetrics: '3.1.2',
            specs2: '3.6.4',
            scalacheck: '1.12.2'
    ]
}

subprojects { subproject ->
    configurations.all {
        resolutionStrategy {
            eachDependency { DependencyResolveDetails details ->
                // Ensures libthrift 0.5.0 isn't accidentally in the classpath
                if (details.requested.group == 'org.apache.thrift') {
                    details.useVersion '0.9.0' // https://github.com/twitter/scrooge/issues/203
                }
                // Don't use mixed versions of SLF4J or Logback
                if (details.requested.group == 'org.slf4j') {
                    details.useVersion commonVersions.slf4j
                }
                if (details.requested.group == 'ch.qos.logback') {
                    details.useVersion commonVersions.logback
                }
            }
        }

        // Some dependencies early bound slf4j impl
        exclude module: "log4j-over-slf4j"
        exclude module: "slf4j-log4j12"
        exclude module: "slf4j-logback"
    }
}